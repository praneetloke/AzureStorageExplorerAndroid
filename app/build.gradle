apply plugin: 'com.android.application'

android {
    repositories {
        mavenCentral()
    }

    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE'] = code.toString()

        //save the updated version code
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.pl.azurestorageexplorer"
            minSdkVersion 16
            targetSdkVersion 25
            versionCode code
            versionName "1.0.0"
            testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        }
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles android.getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:design:25.3.0'
    compile 'com.android.support:support-v4:25.3.0'
    compile 'com.android.support:support-annotations:25.3.0'

    compile 'com.microsoft.azure.android:azure-storage-android:0.7.0@aar'
    compile ('com.github.praneetloke:android-oauth-client:-SNAPSHOT') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-v4'
    }
    compile('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'com.google.android.google-play-services', module: 'google-play-services'
        exclude group: 'junit', module: 'junit'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.android', module: 'android'
    }
    compile('com.google.oauth-client:google-oauth-client-java6:1.22.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    compile('com.google.http-client:google-http-client-jackson:1.22.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    compile 'com.squareup.okhttp3:okhttp:3.4.0-RC1'
    compile('com.google.code.gson:gson:2.7') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    compile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}