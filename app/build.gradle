apply plugin: 'com.android.application'

android {
    repositories {
        mavenCentral()
    }

    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE'] = code.toString()

        def name = ""
        def currentDate = new Date().format("yyyyMMdd")
        if (versionProps.containsKey("VERSION_NAME")) {
            def currentVersionName = versionProps.getProperty("VERSION_NAME").toString()
            def parts = currentVersionName.split("\\.")
            // if we are still in the current date, then just increment the build number
            if (parts.length > 0 && parts[0] == currentDate) {
                name = parts[0] + "." + ((parts[1].toInteger() + 1).toString())
            }
        }

        if (name == "") {
            name = currentDate + ".1"
        }

        // store the version name in the props map
        versionProps['VERSION_NAME'] = name

        //save the updated version code
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.pl.azurestorageexplorer"
            minSdkVersion 21
            targetSdkVersion 27
            versionCode code
            versionName name
            testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        }
    }
    else {
        throw new GradleScriptException("Could not read version.properties!")
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles android.getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:customtabs:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:support-annotations:27.1.1'

    implementation 'com.microsoft.azure.android:azure-storage-android:1.0.0'

    implementation ('com.github.praneetloke:android-oauth-client:-SNAPSHOT') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-v4'
    }
    implementation('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'com.google.android.google-play-services', module: 'google-play-services'
        exclude group: 'junit', module: 'junit'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.android', module: 'android'
    }
    implementation('com.google.oauth-client:google-oauth-client-java6:1.22.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    implementation('com.google.http-client:google-http-client-jackson:1.22.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation('com.google.code.gson:gson:2.7') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.15.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}